apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'launch4j'
apply plugin: 'macAppBundle'
apply plugin: 'gradle-one-jar'

archivesBaseName = "esr-receiver-http"
mainClassName = "ch.luklanis.esreceiver.AppFrame"

sourceCompatibility = 1.6
version = '0.3'


buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'edu.sc.seis.gradle:launch4j:1.0.6'
    classpath 'edu.sc.seis.gradle:macappbundle:2.0.0'
    classpath 'com.github.rholder:gradle-one-jar:1.0.4'
  }
}

macAppBundle {
  dmgName = "${project.archivesBaseName}-macOS-up-to-106.zip"
  mainClassName = 'ch.luklanis.esreceiver.AppFrame'
  appStyle = 'Apple'
  //icon = "myIcon.icns"
  //bundleJRE = true
  //javaProperties.put("apple.laf.useScreenMenuBar", "true")
  //backgroundImage = "doc/macbackground.png"
}

createDmg.enabled = false

//macAppBundleFrom107 {
//  name "${project.archivesBaseName}-macOS-107-and-newer.zip"
//  mainClassName = 'ch.luklanis.esreceiver.AppFrame'
//  appStyle = 'Oracle'
//  //icon = "myIcon.icns"
//  //bundleJRE = true
//  //javaProperties.put("apple.laf.useScreenMenuBar", "true")
//  //backgroundImage = "doc/macbackground.png"
//}


launch4j {
  if (project.hasProperty('launch4jCmd')) {
    launch4jCmd = project.launch4jCmd
  }

  mainClassName = project.mainClassName
  jar = "lib/${project.archivesBaseName}-${project.version}.jar"
  outfile = "${project.archivesBaseName}.exe"
}

repositories {
  mavenCentral()

  maven {
    url "http://maven.mashape.com/releases"
  }
}

dependencies {
  testCompile 'junit:junit:4.+'

  compile 'com.mashape.unirest:unirest-java:1.2.5'
  compile 'org.apache.httpcomponents:httpclient:4.3'
  compile 'org.apache.httpcomponents:httpasyncclient:4.0-beta4'
  compile 'org.apache.httpcomponents:httpmime:4.3'
  compile 'org.json:json:20090211'
  compile 'org.bouncycastle:bcprov-jdk15on:1.49'
}

jar {
    //from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Implementation-Title': 'ESR Receiver',
            'Implementation-Version': version
    }
}

task unitTests(type: Test) {
  include 'ch/luklanis/esreceiver/unit/**/*'
}

task integrationTests(type: Test) {
  include 'ch/luklanis/esreceiver/integration/**/*'
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.8'
}

task zipExe(type: Zip) {
  archiveName = "${project.archivesBaseName}-win.zip"

  into("EsrReceiver") {
    from "$buildDir/launch4j/${launch4j.outfile}"
  }

  into("EsrReceiver/lib") {
    from "$buildDir/launch4j/lib"
  }
}

zipExe.dependsOn createExe

task oneJar(type: OneJar) {
  mainClass = 'ch.luklanis.esreceiver.AppFrame'
  manifest {
    attributes 'Class-Path': './expand'
    attributes 'One-Jar-Expand': 'ESRReceiver.properties,lib/'
    attributes 'One-Jar-Expand-Dir': './expand'
    //attributes 'One-Jar-Confirm-Expand': 'false'
    //attributes 'One-Jar-Show-Expand': 'true'
  }
}

task zipOneJar(type: Zip) {
  dependsOn oneJar

  archiveName = "${project.archivesBaseName}-all.zip"

  from "$buildDir/libs/${oneJar.archiveName}"
}

task buildDists {
  dependsOn build, zipExe, createAppZip, zipOneJar
}
